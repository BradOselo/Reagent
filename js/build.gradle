apply plugin: 'org.jetbrains.kotlin.platform.js'
apply plugin: 'com.moowork.node'

archivesBaseName = 'reagent'

dependencies {
  expectedBy project(':common')
  compile deps.kotlin.stdLib.js
  compile deps.kotlin.coroutines.js
  testCompile deps.kotlin.test.js
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
  from compileKotlin2Js.destinationDir
  configurations.testCompile.each {
    from zipTree(it.absolutePath).matching { include '*.js' }
  }

  into "${buildDir}/node_modules"
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
  kotlinOptions.moduleKind = "commonjs"
}

node {
  download = true
}

task installTestRunner(type: NpmTask) {
  args = ['install', 'mocha']
}

task runTests(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installTestRunner]) {
  script = file('node_modules/.bin/mocha')
  args = [projectDir.toPath().relativize(file(compileTestKotlin2Js.outputFile).toPath())]
}

test.dependsOn(runTests)
